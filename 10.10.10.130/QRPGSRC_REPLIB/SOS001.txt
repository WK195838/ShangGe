     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*--------------------------------------------------------------*
     A*    需求單號   : 1050690                                    *
     A*    PROGRAM ID   : SOS001                                     *
     A*    AUTHOR       : DEREK                                      *
     A*    DATE WRITTEN : 2016/12/26                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售資料建檔子系統                        *
     A*    PROCEDURE    :                                            *
     A*    REMARK       :可試飲收回量                              *
     A****************************************************************
     FSOSDPF  IF  E           K        DISK
     FSOSNLF04IF  E           K        DISK
     FSOSOPF  IF  E           K        DISK
     E*---------------------------------------------------------------*
     I*---------------------------------------------------------------*
     C*===============================================================*
     C*                    PARAMETER
     C*===============================================================*
     C           *ENTRY    PLIST
     C           KSN02     PARM           PS001I 12        單據號碼
     C           KSN17     PARM           PS002I 12        原試飲單號
     C           KSO03     PARM           PS003I  9        產品代號
     C                     PARM           PS001O  50       可收回數量
     C*===============================================================*
     C*                    DEFINE
     C*===============================================================*
     C           *LIKE     DEFN SN02      KSN02            單據號碼
     C           *LIKE     DEFN SN17      KSN17            原試飲單號
     C           *LIKE     DEFN SO03      KSO03            產品代號
     C*===============================================================*
     C*                    KEY LIST
     C*===============================================================*
     C           KEYSD     KLIST
     C                     KFLD           KSN17            原試飲單號
     C                     KFLD           KSO03            產品代號
     C           KEYSO     KLIST
     C                     KFLD           SN02             收回單號
     C                     KFLD           KSO03            產品代號
     C*===============================================================*
     C*                    MAIN
     C*===============================================================*
     C*取得原試飲數量
     C           KEYSD     CHAINSD0                  40
     C           *IN40     IFEQ '0'
     C           SD04      ADD  SD05      WSD045  50
     C                     ELSE
     C                     Z-ADD0         WSD045
     C                     ENDIF
     C*計算累計收回量
     C           KSN17     SETLLSN0
     C           KSN17     READESN0                      46
     C           *IN46     DOWEQ'0'
     C*不計入傳入之收回單號之收回量
     C           SN02      IFNE KSN02
     C           KEYSO     CHAINSO0                  40
     C           *IN40     IFEQ '0'
     C                     ADD  SO05      WSO05   50       確認數量
     C                     ENDIF
     C                     ENDIF
     C           KSN17     READESN0                      46
     C                     ENDDO
     C*計算可收回量
     C           WSD045    IFNE 0
     C           WSD045    SUB  WSO05     PS001O
     C                     ELSE
     C                     Z-ADD0         PS001O
     C                     ENDIF
     C*
     C                     SETON                     LR
