     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250
     H*
     H*    AUTHOR       : IRIS
     H*    CREATE DATE  : 2020/12/25
     H*    UPDATE DATE  :
     H*    PROGRAM NAME : ARA0174
     H*    SYSTEM       :進銷存系統
     H*    SUBSYSTEM    :
     H*    REMARK       :電子發票FTP指令產生-有效B2B (A0401)
     A*===============================================================
     FFTPCMD  O   E                    DISK
     F            FTPCMD                            KRENAMEFTPCMDF
     E                    CMD     1  11 80
     I            DS
     I                                        1  80 MMCMD
     I                                       12  23 MMDTM
     C*------------------------MAIN PROGRAM-----------------------
     C           *LIKE     DEFN SRCSEQ    RRN
     C           *LIKE     DEFN SRCDTA    WCMD
     C           *ENTRY    PLIST
     C                     PARM           PID    10
     C                     PARM           PPWD   10
     C                     PARM           PPTH   30
     C                     PARM           PSDTM  12
     C                     Z-ADD0         RRN
     C                     Z-ADD0         COUNT   20
      *
     C                     ADD  1         RRN
     C                     MOVELPID       WCMD      P
     C                     CAT  PPWD:1    WCMD
     C                     Z-ADDRRN       SRCSEQ
     C                     MOVELWCMD      SRCDTA
     C                     WRITEFTPCMDF
      *
     C           COUNT     DOUEQ11
     C                     ADD  1         RRN
     C                     ADD  1         COUNT
     C           COUNT     IFNE 9
     C           COUNT     ANDNE10
     C                     MOVELCMD,COUNT WCMD      P
     C                     ELSE
     C                     MOVELCMD,COUNT MMCMD     P
     C                     MOVELPSDTM     MMDTM
     C                     MOVELMMCMD     WCMD      P
     C                     ENDIF
     C                     Z-ADDRRN       SRCSEQ
     C                     MOVELWCMD      SRCDTA    P
     C                     WRITEFTPCMDF
     C                     ENDDO
     C                     SETON                     LR
     C*---------------------------------------------------------------
**
NAMEFMT 1
BIN
LCD /EINV/SHGA0401
CD /EINV/SHGA0401/A0401/SRC
DEL A0401_INVOICE_DETAIL.CSV
DEL A0401_INVOICE_MASTER.CSV
MPUT A0401*
CD /EINV/SHGA0401/A0401/DATA
MPUT ARAPPF123456789012*
MPUT ARAQPF123456789012*
QUIT
