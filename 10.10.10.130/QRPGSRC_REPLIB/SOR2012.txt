     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H* PROGRAM NAME : SOR2011                                       *
     H* AUTHER       : DEREK                                         *
     H* CREATE DATE  : 113/04/19                                     *
     H* UPDATE DATE  :                                               *
     H* SYSTEM       :茂世進銷存系統                               *
     H* SUBSYSTEM    :銷售系統                                     *
     H* REMARK       :送貨未確認彙總表                             *
     H*==============================================================*
     H* CODE   AUTHOR.     DATE    DESCRIPTION                    *
     H*--------------------------------------------------------------*
     H*==============================================================*
     FSOR201WFIP  E           K        DISK
     FSOR201F O   E           K        DISK
     IWK0
     I                                              WK06  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I                                      601 605 DSE07S
     I                                      606 610 DSE07E
     I                                      611 6160DSE06S
     I                                      617 6220DSE06E
     I                                      623 6300WSE06S
     I                                      631 6380WSE06E
     C*==============================================================*
     C*                    DEFINE
     C*==============================================================*
     C           *LIKE     DEFN WF03      WWF03            數量
     C           *LIKE     DEFN WF04      WWF04            銷貨成本
     C           *LIKE     DEFN WF03      WQTY             數量
     C           *LIKE     DEFN WF04      WCOST            銷貨成本
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100
     C   L1                EXSR R7100
     C                     EXSR R2000
     CL1 10                EXSR R8100
     C*==============================================================*
     C*          R0100 ... INIT
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     SETON                     10
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7100 ... INIT (L1 BREAK)
     C*==============================================================*
     C           R7100     BEGSR
     C*
     C                     Z-ADD0         WWF03            數量
     C                     Z-ADD0         WWF04            銷貨成本
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ... PROCESS
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C*訂購數量+搭贈數量
     C           WK08      ADD  WK09      WQTY
     C*計算單位成本
     C           WK10      ADD  WK11      WCOST
     C                     ADD  WK12      WCOST
     C*計算銷貨成本
     C                     MULT WQTY      WCOST     H
     C*加總數量
     C                     ADD  WQTY      WWF03
     C*加總銷貨成本
     C                     ADD  WCOST     WWF04
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8100 ... 寫入SOR201F - EXCEL (L1 BREAK)
     C*==============================================================*
     C           R8100     BEGSR
     C*
     C                     MOVELWK06      WF01      P      產品編號
     C                     MOVELWK07      WF02      P      產品名稱
     C                     Z-ADDWWF03     WF03             數量
     C                     Z-ADDWWF04     WF04             銷貨成本
     C*
     C                     WRITEWF0
     C*
     C                     ENDSR
