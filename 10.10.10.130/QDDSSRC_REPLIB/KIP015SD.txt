     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : KIP015SD                                   *
     A*    PROGRAM NAME : VIP視窗查詢                              *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  :                                            *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MTMAPF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A*                                     DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES(' ' '/')
     A            SVP02         15   O  6  6
     A            SVP021         5   O    +1
     A            SVP03         20   O    +2
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 61)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 15'VIP視窗查詢'
     A                                      DSPATR(HI)
     A                                  1  2' <KIP015S>  '
     A                                  3  4'請輸入"/"選擇'
     A                                  4  4'/'
     A                                      DSPATR(UL)
     A                                  4  6'  電話號碼   '
     A                                      DSPATR(UL)
     A                                    +1'分機'
     A                                      DSPATR(UL)
     A                                    +2'      姓名      '
     A                                      DSPATR(UL)
     A            KVP02         15   B  5  6
     A                                      DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A            KVP021         5   B    +1
     A                                      DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
