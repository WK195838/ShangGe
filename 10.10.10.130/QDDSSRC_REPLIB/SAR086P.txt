     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR085P                                    *
     A*    AUTHOR       : JONATHAN YEH                               *
     A*    CREATE DATE  : 103/03/17                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SAR085                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :滯銷產品明細表                            *
     A* 服務單號: NOTE:1030317    WEB:1030360                    *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    88MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    94'滯銷產品明細表'
     A                                      SPACEA(2)
     A                                   164'日期:'
     A            $EGMDY         6  0O   173EDTCDE(Y)
     A                                   183'頁次:'
     A                                   192PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   164'時間:'
     A                                   173TIME
     A                                      SPACEA(1)
     A                                   165'USER :'
     A            $USER         10   O   173
     A                                   183'<SAR086P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================'
     A                                      SPACEA(1)
     A                                     1'產品代號'
     A                                    15'產品名稱'
     A                                    47'保稅倉數量'
     A                                    62'完稅倉數量'
     A                                    76'合計數量'
     A                                    88'最近銷售日'
     A                                      SPACEA(1)
     A                                    47'------------'
     A                                    62'------------'
     A                                    76'----------'
     A                                      SPACEA(1)
     A                                     1'------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ----------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  20                                 1' '
     A                                      SPACEA(1)
     A*產品代號
     A            PFH01          9         1
     A*產品名稱
     A            PFH02         34O       11
     A*保稅倉數量
     A            PFH03          6S 0     49
     A                                      EDTCDE(J)
     A*完稅倉數量
     A            PFH04          6S 0     64
     A                                      EDTCDE(J)
     A*合計數量
     A            PFH05          7S 0     76
     A                                      EDTCDE(J)
     A*最近銷售日
     A            PSLDD          6  0O    88
     A                                      EDTCDE(Y)
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A                                     1'總　　計  --------------->'
     A            TFH03T         7S 0     49
     A                                      EDTCDE(J)
     A            TFH04T         7S 0     64
     A                                      EDTCDE(J)
     A            TFH05T         8S 0     76
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================'
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================'
     A                                      SPACEA(1)
     A                                     2'<<SAR085>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
