     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR085D                                    *
     A*    AUTHOR       : JONATHAN YEH                               *
     A*    CREATE DATE  : 103/03/11                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SAR085                                     *
     A*    SYSTEM       :                                        *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :業務同仁銷售毛利報表(總表)              *
      *   服務單號: NOTE:1030308    WEB:1030319                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR085'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'業務同仁銷售毛利報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'業務員代號:'
     A            DSD03S         6A  B  6 38
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(UL)
     A                                  6 45'~'
     A            DSD03E         6A  B  6 47
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(UL)
     A                                  8 24'客戶代號　:'
     A            DSC04S         5A  B  8 38
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A            DSC05S         4A  B  8 44
     A                                  8 49'~'
     A            DSC04E         5A  B  8 51
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A            DSC05E         4A  B  8 57
     A                                 10 24'確認日期　:'
     A            DSC39S         6  0B 10 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 48'~'
     A            DSC39E         6  0B 10 50
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 61'(MM/DD/YY)'
     A                                 12 24'送貨日期  :'
     A            DSC08S         6  0B 12 38EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 12 48'~'
     A            DSC08E         6  0B 12 50EDTWRD('  /  /  ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                 12 61'(MM/DD/YY)'
     A                                 14 24'報表選項  :'
     A            PRTOPT         1  0B 14 38VALUES(1  2)
     A                                 14 41'(1.業務員 2.客戶)'
     A                                 16 24'執行環境  :'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 24'列印份數'
     A                                 17 36':'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 24'列表機'
     A                                      DSPATR(HI)
     A                                 18 36':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  86        ERR13         50A  O 24  2MSGID(UPT 1016 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
