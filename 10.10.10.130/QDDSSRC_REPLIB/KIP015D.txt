     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP15D                                     *
     A*    AUTHOR       : JONATHAN YEH                               *
     A*    CREATE DATE  : 103/12/24                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :                                          *
     A*===============================================================
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*                                     REF(*LIBL/POCAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            VIPDP          5   O  9  5
     A            VIPCE         15   O  9 11
     A            VIPCE2         5   O  9 27
     A            VIPNM         42   O  9 33
     A*           CA01      R        H
     A*           CA02      R        H
     A*           CA06      R        H
     A*           CA07      R        H
     A*           CA08      R        H
     A*           CA09      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIP015'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'直客(VIP)資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A*                                 4  1'專櫃代號:'
     A*                                     COLOR(BLU)
     A*           DBGN1     R        B  4 13REFFLD(CA01)
     A*           S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A*                                     COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'2=修改'
     A                                      COLOR(BLU)
     A                                    +2'4=刪除'
     A                                      COLOR(BLU)
     A                                    +2'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A*           DBDEP          5   B  8  5
     A            DBCEL         15   B  8 11
     A            DBCEL2         5   B  8 27
     A*           DBNME         42O  B  8 33
     A                                  7  3'A專櫃連絡電話     -
     A                                       分機姓名              -
     A                                      '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A*                                24 66MSGCON(014 USC9998 *LIBL/REMF)
     A*                                     DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  96        ERR1          63A  O 23  2MSGID(URE 0004 REMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 23  2MSGID(URE 0005 REMF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            D#AL9         10   H
     A            D#AL92        10   H
     A                                  1  2'KIP015'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 29'直客(VIP)資料維護 '
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'專櫃代號  :'
     A                                      COLOR(BLU)
     A            DVP01          5A  B  5 16
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#B03         16O     5 22
     A                                      COLOR(BLU)
     A                                  6  2'連絡電話  :'
     A                                      COLOR(BLU)
     A            DVP02         15   B  6 16
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                    +1'分機號碼:'
     A                                      COLOR(BLU)
     A            DVP021         5   B    +1
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                    +1'(若無分機則空白)'
     A                                      COLOR(BLU)
     A                                  7  2'客戶姓名  :'
     A                                      COLOR(BLU)
     A            DVP03         42O  B  7 16
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'出生月份  :'
     A                                      COLOR(BLU)
     A            DVP041         2  0B  8 16
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'出生日期  :'
     A                                      COLOR(BLU)
     A            DVP042         2  0B    +1
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'性  別  :'
     A                                      COLOR(BLU)
     A            DVP05          1A  B 10 16
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 18'(M.男 F.女" ".法人)'
     A                                      COLOR(BLU)
     A                                 11  2'聯絡地址  :'
     A                                      COLOR(BLU)
     A            DVP06         62O  B 11 16
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'服務單位統一編號:'
     A                                      COLOR(BLU)
     A            DVP07         42O  B 12 22
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  3'E-MAIL:'
     A                                      COLOR(BLU)
     A            DVP08         30A  B 13 11
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'有效客戶:'
     A                                      COLOR(BLU)
     A            DVP14          1A  B    +1
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(A.有效 B.無效)'
     A                                      COLOR(BLU)
     A                                 16  2'活動通知:'
     A                                      COLOR(BLU)
     A            DVP09          1A  B 16 14
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(Y/N)'
     A                                      COLOR(BLU)
     A                                 17  2'活動通知為Y時，以下才需要變更'
     A                                      COLOR(RED)
     A                                 18  2'手機簡訊:'
     A                                      COLOR(BLU)
     A            DVP10          1A  B 18 14
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 16'DM通知:'
     A                                      COLOR(BLU)
     A            DVP11          1A  B 18 26
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 29'EMAIL通知:'
     A                                      COLOR(BLU)
     A            DVP12          1A  B    +1
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'電話通知:'
     A                                      COLOR(BLU)
     A            DVP13          1A  B    +1
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(V.選取 " ".不選)'
     A                                      COLOR(BLU)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A*                                24 66MSGCON(014 URE9998 *LIBL/REMF)
     A*                                     DSPATR(HI)
     A*                                     COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B  8  3VALUES(' ' '1' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A*           VIPCE2        20   B  8  6
     A* 72                                  DSPATR(PC)
     A* 72                                  DSPATR(RI)
     A* 32                                  DSPATR(UL)
     A*N32                                  DSPATR(PR)
     A*           VIPNM2        18O  B  8 27
     A*           DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'VPA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'直客(VIP)資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號  :'
     A                                      COLOR(BLU)
     A            DCA01          5A  O  4 15
     A            D#B03     R        O  4 18REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'1=新增'
     A                                      COLOR(BLU)
     A                                  5 26'2=修改'
     A                                      COLOR(BLU)
     A                                  5 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  7  3'A 行動電話         姓名'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A*                                24 66MSGCON(014 URE9998 *LIBL/REMF)
     A*                                     DSPATR(HI)
