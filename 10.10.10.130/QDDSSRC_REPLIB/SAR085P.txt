     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR085P                                    *
     A*    AUTHOR       : JONATHAN YEH                               *
     A*    CREATE DATE  : 103/03/13                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SAR085                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :業務同仁銷售毛利報表(總表)              *
      *   服務單號: NOTE:1030308    WEB:1030319                    *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    88MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    91'業務同仁銷售毛利報表'
     A                                      SPACEA(2)
     A                                     1'業務員代號:'
     A            DSD03S         6        +1
     A                                    +1'-'
     A            DSD03E         6        +1
     A                                   164'日期:'
     A            $EGMDY         6  0O   173EDTCDE(Y)
     A                                   183'頁次:'
     A                                   192PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'客戶代號: '
     A            DSC04S    R             +1REFFLD(SC04)
     A            DSC05S    R             +1REFFLD(SC05)
     A                                    +1'-'
     A            DSC04E    R             +1REFFLD(SC04)
     A            DSC05E    R             +1REFFLD(SC05)
     A                                   164'時間:'
     A                                   173TIME
     A                                      SPACEA(1)
     A                                   165'USER :'
     A            $USER         10   O   173
     A                                   183'<SAR085P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================'
     A                                      SPACEA(1)
     A                                     1'業務員名稱'
     A                                    15'客戶代號'
     A                                    27'客戶簡稱'
     A                                    47'銷售金額'
     A                                    61'銷退金額'
     A                                    75'銷貨成本'
     A                                    98'毛　　利'
     A                                      SPACEA(1)
     A                                    47'----------'
     A                                    61'----------'
     A                                    75'----------'
     A                                    98'----------'
     A                                      SPACEA(1)
     A                                     1'------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ----------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  20                                 1' '
     A                                      SPACEA(1)
     A*業務員名稱
     A            PFH01         12O        1
     A*客戶代號
     A            PFH02     R             17REFFLD(WFH02 *LIBL/REWF100)
     A*客戶簡稱
     A            PFH03     R             27REFFLD(ME04)
     A*銷售金額
     A            PFH04          9  0     47
     A                                      EDTCDE(J)
     A*銷退金額
     A            PFH05          9  0     61
     A                                      EDTCDE(J)
     A*銷貨成本
     A            PFH06         11  2     75
     A                                      EDTCDE(J)
     A*毛利
     A            PFTT1         14  2     93
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A            WFH01A        12O        1
     A                                    +0'小計--------------->'
     A            TFH04         10  0     45
     A                                      EDTCDE(J)
     A            TFH05         10  0     59
     A                                      EDTCDE(J)
     A            TFH06         12  2     73
     A                                      EDTCDE(J)
     A            TFTT1         15  2     91
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT2                       SPACEA(1)
     A                                     1'總　　計  --------------->'
     A            TFH04T        10  0     45
     A                                      EDTCDE(J)
     A            TFH05T        10  0     59
     A                                      EDTCDE(J)
     A            TFH06T        12  2     73
     A                                      EDTCDE(J)
     A            TFTT2         15  2     91
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================'
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ================'
     A                                      SPACEA(1)
     A                                     2'<<SAR085>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
