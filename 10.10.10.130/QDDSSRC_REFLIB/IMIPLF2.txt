     A*****************************************************************
     A*          進貨單據主檔(IMIPLF1)     KEY=IP05+IP04+IP02
     A*****************************************************************
     A          R PF0                       PFILE(POPFPF)
     A            IP01      R               COLHDG('公司別')
     A                                      RENAME(PF01)
     A            IP02      R               COLHDG('驗收單號')
     A                                      RENAME(PF02)
     A            IP021          1   I      COLHDG('單號第四碼')
     A                                      SST(PF02 4 1)
     A            IP03      R               COLHDG('保稅物流點')
     A                                      RENAME(PF06)
     A            IP04      R               COLHDG('完稅物流點')
     A                                      RENAME(PF07)
     A            IP05      R               COLHDG('完稅日期')
     A                                      RENAME(PF04)
     A            IP06      R               COLHDG('倉庫確認日')
     A                                      RENAME(PF11)
     A            IP07      R               COLHDG('結帳註記')
     A                                      RENAME(PF13)
     A          K IP04
     A          K IP05                      DESCEND
     A          K IP02
     A          O IP06                      COMP(NE 0)
     A          O IP07                      COMP(EQ 'V')
     A          O IP021                     COMP(LT '0')
     A*
     A          R SN0                       PFILE(SOSNPF)
     A            IP01      R               COLHDG('公司別')
     A                                      RENAME(SN01)
     A            IP02      R               COLHDG('單據號碼')
     A                                      RENAME(SN02)
     A            IP021          1   I      COLHDG('單號第四碼')
     A                                      SST(SN02 4 1)
     A            IP03      R               COLHDG('出貨物流點代號')
     A                                      RENAME(SN03)
     A            IP04      R               COLHDG('收貨物流點代號')
     A                                      RENAME(SN04)
     A            IP05      R               COLHDG('收回日期')
     A                                      RENAME(SN05)
     A            IP06      R               COLHDG('實際入庫日')
     A                                      RENAME(SN06)
     A            IP07      R               COLHDG('轉帳註記')
     A                                      RENAME(SN10)
     A          K IP04
     A          K IP05                      DESCEND
     A          K IP02
     A          O IP06                      COMP(NE 0)
     A          O IP07                      COMP(EQ 'V')
     A          O IP07                      COMP(EQ 'T')
     A          O IP07                      COMP(EQ 'N')
     A          O IP021                     COMP(LT '0')
     A*
     A          R II0                       PFILE(IMIIPF)
     A            IP01      R               COLHDG('出貨公司')
     A                                      RENAME(II01)
     A            IP011     R               COLHDG('收貨公司')
     A                                      RENAME(II09)
     A            IP02      R               COLHDG('單據號碼')
     A                                      RENAME(II02)
     A            IP021          1   I      COLHDG('單號第四碼')
     A                                      SST(II02 4 1)
     A            IP03      R               COLHDG('出貨物流點代號')
     A                                      RENAME(II03)
     A            IP04      R               COLHDG('收貨物流點代號')
     A                                      RENAME(II04)
     A            IP05      R               COLHDG('調撥日期')
     A                                      RENAME(II05)
     A            IP06      R               COLHDG('實際入庫日')
     A                                      RENAME(II06)
     A            IP07      R               COLHDG('轉帳註記')
     A                                      RENAME(II08)
     A          K IP04
     A          K IP05                      DESCEND
     A          K IP02
     A          O IP06                      COMP(NE 0)
     A          O IP07                      COMP(EQ 'V')
     A          O IP07                      COMP(EQ 'N')
     A          O IP021                     COMP(LT '0')
