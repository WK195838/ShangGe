     A*****************************************************************
     A*          庫存異動明細檔(IMIRLF)
     A*================================================================
     A* #A01 = IMIRLF
     A* #A02 = IMIRLF'S RECORD NAME (EX. PE0, PG0 ......)
     A* #A03  第一碼: '+' -該數量屬　進貨
     A*                 '-' -該數量屬　出貨
     A*                 ' ' -該數量屬　進或出視 IR09而定
     A*                 '*' -該數量為正者　屬進貨
     A*                      該數量為負者　屬出貨
     A*       第二碼: 'Y' -有成本資料
     A*                 'N' -無成本資料
     A*       第三碼: 'T' -該成本資料為總成本
     A*                 'U' -該成本資料為單位成本
     A****
     A*================================================================
     A*序號|代號|      檔　案　名　稱      |   #A03   | RCD NAME|
     A*======|======|============================|==========|=========|
     A*  02  |POPGPF|國外採購完稅明細檔 -保稅| －ＹＴ | PG0     |
     A*  05  |SOSDPF|銷售訂單明細檔            | －ＹＵ | SD0     |
     A*  06  |SOSFPF|送貨單明細檔 - 出貨     | －ＹＵ | SF0     |
     A*  09  |SOSOPF|產品收回明細檔 - 出貨   | －ＹＴ | SO0     |
     A*  11  |IMIFPF|產品報廢明細檔            | －ＹＴ | IF0     |
     A*  12  |IMIHPF|調整明細檔 - 調出       | －ＹＴ | IH0     |
     A*  14  |IMIJPF|存貨調撥明細檔 - 出貨   | －ＹＴ | IJ0     |
     A*  16  |IMILPF|禮盒組拆明細檔            | 　ＹＴ | IL0     |
     A*****************************************************************
     A*--------------------------------------
     A*禮盒組拆明細檔(IMILPF)  　Ｎ
     A*--------------------------------------
     A          R IL0                       PFILE(IMILPF)
     A            IR01           1   I      COLHDG('公司別第一碼')
     A                                      SST(IL01 1 1)
     A            IR02      R               COLHDG('物流點')
     A                                      RENAME(IL03)
     A            IR03      R               COLHDG('產品編號')
     A                                      RENAME(IL05)
     A            IR04      R               COLHDG('組拆日期')
     A                                      RENAME(IL04)
     A            IR05           2   I      COLHDG('單別')
     A                                      SST(IL02 3 2)
     A            IR051          1   I      COLHDG('單別')
     A                                      SST(IL02 4 1)
     A            IR06      R               COLHDG('單據號碼')
     A                                      RENAME(IL02)
     A            IR071     R    6S 0       COLHDG('數量')
     A                                      RENAME(IL06)
     A            IR09      R               COLHDG('進／出')
     A                                      RENAME(IL07)
     A*                                     +1 -進
     A*                                     -1 -出
     A            IR121     R    6S 0       COLHDG('確認量')
     A                                      RENAME(IL06)
     A          K IR03
     A          K IR02
     A          K IR01
     A          K IR04                      DESCEND
     A          K IR06
     A          O IR051                     COMP(LT '0')
     A*
