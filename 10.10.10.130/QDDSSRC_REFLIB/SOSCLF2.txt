     A*****************************************************************
     A*          銷售訂單主檔(SOSCLF1)     KEY=SC07+SC02
     A*****************************************************************
     A          R SC0                       PFILE(SOSCPF)
     A            SC01      R               COLHDG('公司別')
     A            SC02      R               COLHDG('訂單號碼')
     A*                         XX   -  XX  -    XX    - XXXXXX
     A*                       公司 -單別-西元年份-序號
     A            SC03      R               COLHDG('單別')
     A            SC04      R               COLHDG('客戶代號')
     A            SC05      R               COLHDG('分店代號')
     A            SC06      R               COLHDG('客戶訂單號碼')
     A            SC07      R               COLHDG('訂單日期')
     A            SC08      R               COLHDG('預計送貨日期')
     A            SC09      R               COLHDG('發票聯數')
     A            SC10      R               COLHDG('試飲原因代碼')
     A            SC11      R               COLHDG('出貨物流點')
     A            SC11L          1   I      COLHDG('出貨物流點第一碼')
     A                                      SST(SC11 1 1)
     A            SC12      R               COLHDG('總金額')
     A            SC13      R               COLHDG('訂單狀態')
     A**                                    非寄賣訂單
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A*                                     'V' -已送貨確認
     A**                                    寄賣訂單
     A*                                     'D' -發票作廢待重開
     A            SC14      R               COLHDG('發票地址－總店')
     A            SC15      R               COLHDG('發票地址－分店')
     A            SC16      R               COLHDG('送貨地址－總店')
     A            SC17      R               COLHDG('送貨地址－分店')
     A            SC18      R               COLHDG('訂單附註１')
     A            SC19      R               COLHDG('訂單附註２')
     A            SC20      R               COLHDG('訂單附註３')
     A            SC21      R               COLHDG('核準備註')
     A            SC22      R               COLHDG('單價待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A            SC23      R               COLHDG('額度待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SC24      R               COLHDG('逾期待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SC25      R               COLHDG('搭贈待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SC26      R               COLHDG('試飲待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A            SC27      R               COLHDG('業務人員')
     A*                                     FROM客戶主檔
     A            SC28      R               COLHDG('業務員組別')
     A*                                     FROM員工主檔於新增代入
     A*                                         修改時不更新
     A            SC29      R               COLHDG('送貨單號')
     A            SC30      R               COLHDG('發票號碼')
     A            SC31      R               COLHDG('客戶地區')
     A            SC32      R               COLHDG('客戶區域')
     A            SC33      R               COLHDG('通路')
     A            SC34      R               COLHDG('型態')
     A            SC35      R               COLHDG('轉帳註記')
     A*                                     'V' -已轉帳
     A*                                     ' ' -尚未轉帳
     A*                                     '*' -公司間買賣CL->RE
     A*                                            無須轉帳
     A*                                     'T' -轉帳訂單
     A*                                     'N' -無須轉帳(轉帳處理時更新)
     A            SC36      R               COLHDG('進貨物流點')
     A            SC37      R               COLHDG('課稅別')
     A*                                     'D' -應稅
     A*                                     'B' -免稅
     A*                                     'Z' -零稅
     A            SC38      R               COLHDG('結帳註記')
     A**                                    'V'-已結帳
     A            SC39      R               COLHDG('確認日期')
     A            SCXX      R               COLHDG('異動日期')
     A            SCYY      R               COLHDG('異動時間')
     A            SCZZ      R               COLHDG('異動者　')
     A          K SC08                      DESCEND
     A**             ('送貨日期')
     A          K SC02
     A**             ('訂單號碼')
     A          O SC11L                     COMP(NE 'V')
