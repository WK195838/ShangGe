     A*****************************************************************
     A*          送貨單主檔(SOSELF01)    KEY=SE06+SE02
     A*****************************************************************
     A          R SE0                       PFILE(SOSEPF)
     A            SE01      R               COLHDG('公司別')
     A            SE02      R               COLHDG('送貨單號')
     A*                         XX   -  XX  -    XX    - XXXXXX
     A*                       公司 -單別-西元年份-序號
     A            SE03      R               COLHDG('單別')
     A            SE04      R               COLHDG('客戶代號')
     A            SE05      R               COLHDG('分店代號')
     A            SE06      R               COLHDG('交貨日期')
     A            SE07      R               COLHDG('出貨物流點')
     A            SE07L          1   I      COLHDG('出貨物流點第一碼')
     A                                      SST(SE07 1 1)
     A            SE08      R               COLHDG('總金額')
     A            SE09      R               COLHDG('送貨單狀態')
     A*                                     '*' -待核準
     A*                                     'V' -已送貨確認
     A*                                     'C' -電腦核準
     A*                                     ' ' -已核準未送貨
     A            SE10      R               COLHDG('送貨地址－總店')
     A            SE11      R               COLHDG('送貨地址－分店')
     A            SE12      R               COLHDG('備註１')
     A            SE13      R               COLHDG('備註２')
     A            SE14      R               COLHDG('備註３')
     A            SE15      R               COLHDG('單價待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SE16      R               COLHDG('額度待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SE17      R               COLHDG('逾期待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SE18      R               COLHDG('搭贈待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A*                                     'C' -電腦核準
     A            SE19      R               COLHDG('換貨待核準註記')
     A*                                     '*' -待核準
     A*                                     ' ' -已核準
     A            SE20      R               COLHDG('列印序號')
     A            SE21      R               COLHDG('派車日期')
     A            SE22      R               COLHDG('派車車號')
     A            SE23      R               COLHDG('車次')
     A            SE24      R               COLHDG('確認日期')
     A            SE25      R               COLHDG('列印註記')
     A*                                     'V' -已列印
     A*                                     ' ' -未列印
     A            SE26      R               COLHDG('收貨物流點')
     A            SE27      R               COLHDG('通路')
     A            SE28      R               COLHDG('型態')
     A            SE29      R               COLHDG('訂單號碼')
     A            SE30      R               COLHDG('發票號碼')
     A            SE31      R               COLHDG('發票狀態')
     A*                                     '*' -發票已作廢待重開
     A            SE32      R               COLHDG('業務員')
     A            SE33      R               COLHDG('部門別')
     A            SE34      R               COLHDG('地區')
     A            SE35      R               COLHDG('區域')
     A            SE36      R               COLHDG('轉帳註記')
     A*                                     'V' -已轉帳
     A*                                     ' ' -尚未轉帳
     A*                                     '*' -公司間買賣CL->RE
     A*                                            無須轉帳
     A*                                     'T' -轉帳送貨單
     A*                                     'N' -無須轉帳(轉帳處理時更新)
     A            SE37      R               COLHDG('原總金額')
     A            SE38      R               COLHDG('課稅別')
     A*                                     'D' -應稅
     A*                                     'B' -免稅
     A*                                     'Z' -零稅
     A            SE39      R               COLHDG('送貨單日期')
     A            SE40      R               COLHDG('結帳註記')
     A**                                    'V'-已結帳
     A            SE41      R               COLHDG('發票地址－總店')
     A            SE42      R               COLHDG('發票地址－分店')
     A            SEXX      R               COLHDG('異動日期')
     A            SEYY      R               COLHDG('異動時間')
     A            SEZZ      R               COLHDG('異動者　')
     A          K SE06                      DESCEND
     A**              ('送貨日期')
     A          K SE02
     A**              ('送貨單號')
     A          O SE07L                     COMP(NE 'V')
